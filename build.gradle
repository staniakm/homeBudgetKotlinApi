import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
	id 'org.springframework.boot' version '3.5.11'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jetbrains.kotlin.jvm' version '2.2.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
	id 'com.google.cloud.tools.jib' version '3.5.3'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.18'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

kotlin {
	jvmToolchain(17)
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.postgresql:postgresql'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'io.kotest:kotest-runner-junit5-jvm:6.1.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jib {
	def tag = "${version}"
	from {
		image = "gcr.io/distroless/java17-debian12"
	}
	to {
		image = "${rootProject.name}:${tag}"
	}
}

tasks.withType(KotlinJvmCompile).configureEach {
	compilerOptions {
		freeCompilerArgs.add('-Xjsr305=strict')
		freeCompilerArgs.add('-Xannotation-default-target=param-property')
		jvmTarget = JvmTarget.JVM_17
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.named('jacocoTestReport')
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.named('test')
	reports {
		xml.required = true
		html.required = true
	}
}

tasks.register('forbidDisabledTests') {
	group = 'verification'
	description = 'Fails build when @Disabled is used in tests'
	doLast {
		def disabledPattern = ~/(^|\s)@Disabled(\s*\(|\b)/
		def offenders = fileTree('src/test').matching {
			include '**/*.kt', '**/*.java'
		}.files.findAll { it.text =~ disabledPattern }

		if (!offenders.isEmpty()) {
			def files = offenders.collect { project.relativePath(it) }.sort().join('\n - ')
			throw new GradleException("@Disabled is not allowed in tests.\n - ${files}")
		}
	}
}

tasks.named('jacocoTestCoverageVerification') {
	dependsOn tasks.named('test')
	violationRules {
		rule {
			element = 'BUNDLE'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.95
			}
		}
		rule {
			element = 'PACKAGE'
			includes = ['com.example.demo.controller', 'com.example.demo.repository', 'com.example.demo.service']
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}
		}
	}
}

tasks.named('check') {
	dependsOn tasks.named('forbidDisabledTests')
	dependsOn tasks.named('jacocoTestCoverageVerification')
}
